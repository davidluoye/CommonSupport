apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    // archiveClassifier need gradle version >= 5.1
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.source
    exclude "**/R.class"
    exclude "**/BuildConfig.class"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'io.github.davidluoye'
            artifactId 'lib-common'
            version '2.5.0'

            artifact("$buildDir/outputs/aar/${project.getName()}.aar")
            artifact androidSourcesJar
            pom {
                // 构件名称，可以自定义
                name = 'lib-common'
                // 构件描述
                description = 'Android common util library.'
                // 构件主页
                url = 'https://github.com/davidluoye/CommonSupport'
                // 许可证名称和地址
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                // 开发者信息
                developers {
                    developer {
                        name = 'Yangdw'
                        email = 'davidluoye@gmail.com'
                    }
                }
                // 版本控制仓库地址
                scm {
                    url = 'https://github.com/davidluoye/CommonSupport'
                    connection = 'scm:git:github.com/davidluoye/CommonSupport.git'
                    developerConnection = 'scm:git:ssh://git@github.com/davidluoye/CommonSupport.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                // username and password should be place in global properties in ~/.gradle/gradle.properties in linux.
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications
}
